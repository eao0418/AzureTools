ApplicationRegistration
    | where RecordGenerateTime > ago(1d)
        and (isnotempty( PasswordCredentials) and PasswordCredentials != "[]")
    | project Id, CreatedDateTime, DisplayName, PasswordCredentials, TenantId, RecordGenerateTime, ExecutionId
    | summarize arg_max(RecordGenerateTime, *) by Id
    | mv-expand PasswordCredentials
    | extend
        passwordDisplayName = tostring(PasswordCredentials.displayName),
        passwordEndDateTime = todatetime(PasswordCredentials.endDateTime),
        keyId = tostring(PasswordCredentials.keyId)
    | extend
        days_until_expiration = datetime_diff( 'day', passwordEndDateTime, now()),
        // checks to see if automation has made a new version of this password.
        passDisplayParts = split(passwordDisplayName, "_")
    | extend 
        passwordDisplayName = tostring(passDisplayParts[0]),
        passwordVersion = iff(array_length( passDisplayParts) > 1, unixtime_milliseconds_todatetime(tolong(passDisplayParts[1])), todatetime(PasswordCredentials.startDateTime))
    | summarize arg_max(passwordVersion, *) by Id
    | extend days_until_expiration = datetime_diff('day', passwordEndDateTime, now())
    | where (days_until_expiration < 4 and passwordEndDateTime > now())
        or (passwordEndDateTime < now() and passwordVersion == PasswordCredentials.startDateTime)
    | project Id, TenantId, DaysUntilExpiration = days_until_expiration, DisplayName
;